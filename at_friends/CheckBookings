#! /bin/csh
#-----------------------------------------------------------------------------
# $Id$
#-----------------------------------------------------------------------------

  set DIR = $MIR/at_friends
  source  /u/support/BOOK/BOOKENV.CSH

# Generate a list of known users.
  rm -f bookings.users
  awk '{s = substr($1,0,1); if (s >= "a" && s <= "z") print $1;}' \
    $DIR/users.txt | sort -u > bookings.users

# Generate a list of users with bookings.
  rm -f bookings.current
  touch bookings.current
  foreach file ($BOOK/DATA/*.DIARY)
    awk 'BEGIN {arm = 0} \
               {if (arm && $1 != "#" && $4 != "" && $4 != ";") print $4} \
         $1 == ";" {arm = 1}' $file | sed -e 's/;//g' >> bookings.current
  end

# Send mail if there is someone new.
  rm -f bookings.newusers
  sort -u bookings.current | comm -1 -3 bookings.users - > bookings.newusers

  if (! -z bookings.newusers) then
    rm -f bookings.mail
    touch bookings.mail
    foreach user (`cat bookings.newusers`)
      grep -h $user $BOOK/DATA/*.DIARY >> bookings.mail
    end

    echo " " >> bookings.mail
    lynx -dump "http://www.atnf.csiro.au/people/current_visitors.html" | \
      sed -e "s/\[[0-9]*\]//g" -e "/References/q" > $DIR/epp_visitors.txt
    lynx -dump "http://www.narrabri.atnf.csiro.au/~rwark/visitors" > \
      $DIR/nar_visitors.txt
    cat $DIR/epp_visitors.txt >> bookings.mail
    mail -s "New users with bookings" \
      at_friends@atnf.csiro.au,mirmgr@atnf.csiro.au, < bookings.mail
  endif
