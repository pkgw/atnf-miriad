#!/bin/csh -ef
#-----------------------------------------------------------------------------
# Usage: mirimport [-noupdate]
#-----------------------------------------------------------------------------
# Fetch and recompile (unless -noupdate is specified) Miriad updates from the
# anonymous ftp area at the ATNF.
#
# $Id$
#-----------------------------------------------------------------------------
# Initialise.
  set path = ($MIRBIN $path)

# Move to the import directory.
  if (! -e "$MIR/etc") mkdir $MIR/etc
  cd $MIR/etc
  if ($status) then
    echo "### Fatal: Failed to cd to $MIR/etc"
    exit 1
  endif

# Get the list of update files from the anon ftp area.
  touch mirupd-99999999-9.tar.gz
  rm -f mirupdates mirupd-????????-?.tar.gz
  ftp -in << END_OF_FILE >& /dev/null
open ftp.atnf.csiro.au
user ftp mirimport@atnf.csiro.au
cd /pub/software/miriad/updates
get mirupdates
quit
END_OF_FILE

  if (! -f mirupdates) then
    echo "### Fatal: Failed to find list of updates."
    exit 1
  endif

# Determine which updates are needed.
  if (-e $MIR/etc/Last_import) then
    set lastimport = `tail -1 $MIR/etc/Last_import`
  else if (-e $MIR/VERSION) then
    set vers = `tail -1 $MIR/VERSION`
    set lastimport = mirupd-${vers}-0.tar.gz
  else
    set lastimport = mirupd-00000000-0.tar.gz
  endif

  awk 'BEGIN {print "open ftp.atnf.csiro.au"; \
              print "user ftp mirmgr@atnf.csiro.au"; \
              print "cd /pub/software/miriad/updates"; \
              print "binary";} \
             {if ($1 > base) print "get", $1;} \
         END {print "quit";}' base=$lastimport mirupdates > junk.ftp

  set lines = (`wc -l junk.ftp`)
  if ($lines[1] <= 5) then
    echo "### No new updates found"
    exit 0
  endif

# Fetch all required update tar files.
  ftp -in < junk.ftp >& /dev/null

  ls mirupd-????????-?.tar.gz >& /dev/null
  if ($status) then
    echo "### Fatal: Failed to retrieve the required update files"
    exit 1
  endif

  # Untar the updates.
  foreach file (mirupd-????????-?.tar.gz)
    echo Untarred $file at `date`
    zcat $file | (cd $MIR; tar -xvmf -)

    set stat1 = $status
    if ($stat1 == 141) then
      echo "### Warning: Untarring ended with status=$stat1"
    else if ($stat1) then
      echo "### Fatal: Failed to successfully untar - status=$stat1"
      exit 1
    endif

    rm -r $file
    if ("$file" != "mirupd-99999999-9.tar.gz") then
      echo $file >> $MIR/etc/Last_import
    endif
  end

# Recompile as required.
  if ("$1" != -noupdate) gmake
