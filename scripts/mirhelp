#!/bin/csh -f
#
#= mirhelp -- Give help on a Miriad task
#& rjs
#: user utility
#+
#  Usage: mirhelp [-w] [-k keyword] task
#
#  mirhelp gives help on a Miriad task.
#
#  Options:
#    -w    Use the netscape browser to display the help.
#    -k    Get help about a specific task keyword.
#
#--
#  History:
#    Dark-ages rjs  Original version
#    22-feb-01 rjs  Added matching for possible tasks.
#    02-mar-01 rjs  Slight change to make it compatible with more UNIX systems.
#    18-mar-03 rjs  Fixed ordering of honouring the -w and MIRPAGER env. var.
#    31-may-06 rjs  Added mozilla as a browser.
#    28-jun-06 rjs  Added cygstart as a browser (standard web browser in the
#                   cygwin environment).
#
# $Id$
#-----------------------------------------------------------------------------

set webroot = "http://www.atnf.csiro.au/computing/software/miriad"

set noglob

set task = ""
set key  = ""
set doweb = 0
set keyw = 0
set message = ""

foreach arg ($argv)
  set earg = (`echo $arg | sed -e 's/./& /g'`)
  if ($keyw) then
    set key = $arg
    set keyw = 0
  else if ($#earg > 1 && "$earg[1]" == "-") then
    foreach s ($earg[2-])
      if ("$s" == "k") then
        set keyw = 1
      else if ("$s" == "w") then
        set doweb = 1
      else if ("$s" == "?") then
        set message = ""
        goto usage
      else
        set message = "Invalid flags: $arg"
        goto usage
      endif
    end
  else
    set message = "Help on only one task can be given at a time: $arg"
    if ("$task" != "") goto usage
    set task = $arg
  endif
end

if ("$task" == "") set task = "miriad"

if ($doweb) then
  set html = "$webroot/doc/$task.html"
  if  ("$key" != "") set html = "$html#$key"

  if (! $?MIRPAGER) set MIRPAGER = firefox

  if ("$MIRPAGER" == lynx) then
    lynx -use_mouse "$html"
  else if ("$MIRPAGER" == cygstart) then
    cygstart "$html"
  else
    "$MIRPAGER" -remote "OpenURL($html,new-tab)"
    if ($status) "$MIRPAGER" "$html" &
  endif

else
  if ($?MIRPAGER) then
    set mypager = "$MIRPAGER"
  else if ($?PAGER) then
    set mypager = "$PAGER"
  else
    set mypager = more
  endif

  if ($?MIRPDOC == 0) setenv MIRPDOC "$MIR/doc"
  setenv MIRPDOC ".:$HOME/miriad/doc:$MIRPDOC"

  foreach dir (`echo "$MIRPDOC" | sed -e 's/:/ /g'`)
    set file = "$dir/$task.doc"
    if (-f "$file") then
      # Does this override an installed doc file?
      if ("$dir" != "$MIR/doc" && -f "$MIR/doc/$task.doc") set dowarn
      break
    endif
  end

  if (! -f "$file") then
    set matches = ()
    unset noglob
    set nonomatch
    foreach dir (`echo "$MIRPDOC" | sed -e 's/:/ /g'`)
      foreach f ("$dir"/"$task"*.doc)
        set t = $f:t
        set t = $t:r
        if ("$t" != "$task"*) set matches = ($matches $t)
      end
    end
    set noglob
    unset nonomatch

    if ($#matches) then
      echo "### Warning: No task called '$task' was found."
      echo "### Warning: Possible matches are:" \
        `echo $matches | awk '{for (i = 1; i <= NF; i++) print $i}' | sort -u`
    else
      echo "### Fatal: No task matching '$task' was found."
    endif
  else
    if ("$key" != "") set file = "$file -k $key"
    if ($?dowarn) then
      (echo "WARNING: Reading $file" ; docfmt $file) | $mypager
    else
      docfmt $file | $mypager
    endif
  endif
endif
exit 0

usage:
if ("$message" != "") echo "### Fatal: $message"
echo " "
echo "  Usage:"
echo "    mirhelp [-w] [-k keyword] task"
echo " "
echo "  Options:"
echo "   -w   View help in a web browser (set by MIRPAGER)"
echo "   -k   Give help on a specific keyword"
echo " "
echo "  Environment Variables:"
echo "    MIRPAGER  Paging program to be used.  This can be either a text"
echo "              pager (e.g. more, less) or a web browser (firefox,"
echo "              mozilla, iceape, netscape, lynx, etc.)."
echo "    PAGER     A text paging program (e.g. more, less) used if MIRPAGER"
echo "              is not set.  The default is 'more'."
echo "    MIRPDOC   Colon-separated list of directories to search for doc"
echo "              files.  Default is .:$MIR/doc."
