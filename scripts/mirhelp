#!/bin/csh -f
#
#= mirhelp -- Give help on a Miriad task
#& rjs
#: user utility
#+ 
#  Usage: mirhelp [-w] [-k keyword] task
#
#  mirhelp gives help on a Miriad task.
#
#  Options:
#    -w    Use the netscape browser to display the help.
#    -k    Get help about a specific task keyword.
# 
#--

set noglob

set task = ""
set key  = ""
set doweb = 0
set keyw = 0
set message = ""

foreach arg ($argv)
  set earg = ( `echo $arg | sed -e 's/./& /g'` )
  if ( $keyw ) then
    set key = $arg
    set keyw = 0
  else if ( $#earg > 1 && "$earg[1]" == "-" ) then
    foreach s ( $earg[2-] )
      if ( "$s" == "k" ) then
	set keyw = 1
      else if ( "$s" == "w" ) then
	set doweb = 1
      else if ( "$s" == "?" ) then
        set message = ""
	goto usage
      else
	set message = "Invalid flags: $arg"
	goto usage
      endif
    end
  else
    set message = "Help on only one task can be given at a time: $arg"
    if ( "$task" != "" ) goto usage
    set task = $arg
  endif
end

set message = "No task name given"
if ( "$task" == "" ) goto usage

if ( $doweb ) then
  set web = "http://www.atnf.csiro.au/computing/software/miriad/$task.html"
  if  ( "$key" != "" ) set web = "$web#$key"
  netscape -remote "OpenURL($web)"
  if ( $status ) netscape -install "$web"
else
  set file = $MIRPDOC/$task.doc
  if ( -f $task.doc ) set file = $task.doc
  if ( "$key" != "" ) set file = "$file -k $key"
  if ( ! $?PAGER ) set PAGER = "more"
  docfmt $file | $PAGER
endif
exit 0

usage:
if ( "$message" != "" ) echo "### Fatal: $message"
echo " "
echo "  Usage:"
echo "    mirhelp [-w?] [-k keyword] task"
echo " "
echo "  Options:"
echo "   -w   Direct help to netscape browser"
echo "   -?   Give this help message"
echo "   -k   Give help on a specific keyword"
