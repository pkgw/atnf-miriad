#!/bin/csh -f
#
#  Generate a tar file containing files that have changed

set rhost = $1
cd $MIR

# Generate the list of the files to update.

if ( -e $MIRLIB/Last_update_${rhost} ) then
  set cond = (-newer $MIRLIB/Last_update_${rhost} )
else
  set cond = ( )
endif
find cat inc linpack man prog scripts spec specdoc subs tools \
	\! -type d \! -name junk\* \! -name \*.o \
	\! -name \*.log \! -name \*.dvi \! -name \*.def \
	$cond \
	-print > /tmp/${rhost}_files
if ( -z /tmp/${rhost}_files ) exit

echo There are some files to process

# Determine the filename of the output tar file

set date = ( `date` )
set month = $date[2]
if ( $month == "Jan" ) set mon = 1
if ( $month == "Feb" ) set mon = 2
if ( $month == "Mar" ) set mon = 3
if ( $month == "Apr" ) set mon = 4
if ( $month == "May" ) set mon = 5
if ( $month == "Jun" ) set mon = 6
if ( $month == "Jul" ) set mon = 7
if ( $month == "Aug" ) set mon = 8
if ( $month == "Sep" ) set mon = 9
if ( $month == "Oct" ) set mon = 10
if ( $month == "Nov" ) set mon = 11
if ( $month == "Dec" ) set mon = 12

set base = $MIR/export/$rhost
if ( ! -d $MIR/export ) mkdir $MIR/export
if ( ! -d $base ) mkdir $base
set file = $base/${date[6]}_${mon}_${date[3]}-${date[4]}.tar

# Create the output tar file.

tar -cf $file `cat /tmp/${rhost}_files`
compress -f $file

# Remember the time that we did this.

echo Tar file written at `date` by `whoami` >> $MIRLIB/Last_update_${rhost}

# Send a message to let me know what was copied to the tar file.

echo Generated tar file $file, containing
cat /tmp/${rhost}_files
