#!/bin/csh -xf

# doc2html topic

set topic = $argv[1]
set topic = $topic:r
set topic = $topic:t
set out = $topic.html
set sed_script = /tmp/$$.sed

# Is it one of the special topics?

set specials = (cgcoords device in line offset oldflux options out \
		region select server stokes telescop vis window )

set special_topic = 0
foreach special ($specials)
  if ( $topic == $special ) set special_topic = 1
end

# Convert special characters.

echo 's/\&/\&amp;/g' 				>> $sed_script
echo 's/</\&lt;/g' 				>  $sed_script
echo 's/>/\&gt;/g'				>> $sed_script

# Look for HTML links.
set links = (`docfmt $argv[1]|tr -s " " "[\012*]"|grep http://|sort|uniq`)
if ( $#links > 0 ) then
  foreach link ($links)
    echo 's|'$link'|<a href="'$link'">'$link'</a>|' >> $sed_script
  end
endif

# Create links to the special topics.

foreach special ( $specials )
  echo 's/"'$special'"/<a href="'$special'.html">'$special'<\/a>/' >> $sed_script
end
#echo 's/"/ /g' 					>> $sed_script

# If its a special topic, do no more. Otherwise if its a plain topic,
# add in the links to the special topics.

if (! $special_topic ) then
  foreach special ( $specials )
    echo 's/^Key: '$special'$/Key: <a href="'$special'.html">'$special'<\/a>/' >> $sed_script
  end
endif

set user = `whoami`
set user = "$user@atnf.csiro.au"
set date = (`date +"%d %h 19%y"`)

echo "<title>" Documentation on $topic "</title>"			>  $out
echo '<body bgcolor="ffffff">'						>> $out
echo "<h1>"    Documentation on $topic "</h1>"				>> $out
echo "<pre>"								>> $out
docfmt $argv[1] | sed -f $sed_script					>> $out
echo "</pre>"								>> $out
echo "<hr>"								>> $out
echo " "								>> $out
echo "<address>Generated by $user on $date </address>"			>> $out
echo "</body>"								>> $out
rm -f $sed_script
