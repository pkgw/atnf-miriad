#!/bin/csh -f

# doc2html topic

set topic = $argv[1]
set topic = $topic:r
set topic = $topic:t
set out = $topic.html
set sed_script = /tmp/$$.sed

# Is it one of the special topics?

set specials = (device line options offset region \
		select server stokes vis in out)

set special_topic = 0
foreach special ($specials)
  if ( $topic == $special ) set special_topic = 1
end

# Edit out the characters special to HTML (In <pre> mode, commands
# like &lt do not seem to work).

echo 's/</ /g' 					>  $sed_script
echo 's/>/ /g'					>> $sed_script
echo 's/"/ /g' 					>> $sed_script
echo 's/\&/ /g' 				>> $sed_script

# If its a special topic, do no more. Otherwise if its a plain topic,
# add in the links to the special topics.

if (! $special_topic ) then
  foreach special ( $specials )
    echo 's/^Key: '$special'$/Key: <a href="'$special'.html">'$special'<\/a>/' >> $sed_script
  end
endif

set user = `whoami`
set date = (`date +"%d %h 19%y"`)

echo "<title>" Documentation on $topic "</title>"			>  $out
echo "<h1>"    Documentation on $topic "</h1>"				>> $out
echo "<pre>"								>> $out
docfmt $argv[1] | sed -f $sed_script					>> $out
echo "</pre>"								>> $out
echo "<hr>"								>> $out
echo " "								>> $out
echo "<address>Generated by $user on $date </address>"			>> $out

rm -f $sed_script
